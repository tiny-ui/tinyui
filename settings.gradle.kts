import java.util.Properties

pluginManagement {
    includeBuild("build-logic")
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

dependencyResolutionManagement {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.name = "tinyui"

include(":compose")
include(":updates")
// as an included build of a host App only the library is wanted; the sample would drag its Node toolchain and app shell into that composite
if (gradle.parent == null) {
    include(":sample:shared")
    include(":sample:androidApp")
}

// 本地联调 quickjs-kmp：local.properties 写 quickjs-kmp.dir=<仓路径> 即从源码构建，
// 坐标 → 项目路径的映射由该仓 gradle/composite-substitutions 声明；CI 没有 local.properties，解析 Maven 版本。
val localProperties = file("local.properties").takeIf { it.exists() }?.let { f ->
    Properties().apply { f.inputStream().use { load(it) } }
}
localProperties?.getProperty("quickjs-kmp.dir")?.let { dir ->
    val sdkDir = file(dir)
    require(sdkDir.isDirectory) { "quickjs-kmp.dir 不存在: $sdkDir" }
    // 构建脚本据此取该仓的宿主产物（qjsc-kmp、host JNI 库）
    gradle.extra["quickjs-kmp.dir"] = sdkDir
    includeBuild(sdkDir) {
        name = "quickjs-kmp" // 构建脚本按这个名字引用它的任务，不依赖目录名
        dependencySubstitution {
            sdkDir.resolve("gradle/composite-substitutions").readLines()
                .map { it.substringBefore('#').trim() }
                .filter { it.isNotEmpty() }
                .forEach { line ->
                    val (coordinate, projectPath) = line.split("=", limit = 2).map(String::trim)
                    substitute(module(coordinate)).using(project(projectPath))
                }
        }
    }
}
